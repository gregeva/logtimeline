#!/usr/bin/perl
use strict;
use warnings;
use POSIX qw(strftime);
use Time::Piece;
use DateTime;
use Term::ReadKey;
use Getopt::Long;
use Term::ANSIColor;

# Configuration
my ( $bucket_size_minutes, $bucket_size_seconds, $print_seconds, $print_milliseconds, $omit_empty, $total_lines_read, $total_lines_included, $total_lines_highlighted ) = 0;
my ( %log_counts, %category_totals );			# this is where the counts of log entries are tallied across the time buckets
my $legend_length = 0;
my $range_start = "";
my $range_end = "";
my $range_start_epoch = 0;
my $range_end_epoch = 0;
my %range_epoch = ( 'start' => 0, 'end' => 2521843200 );
my %range;
my $output_timestamp_format = "%Y-%m-%d %H:%M";
#my( $output_timestamp_min ) = DateTime->from_epoch( epoch => 1678886400 );
my( $output_timestamp_min, $output_timestamp_max );
my( $exclude_regex, $include_regex, $highlight_regex );
my $pause_output = 0;
my $hide_values = 0;
my @in_files;
my ($terminal_width, $terminal_height) = GetTerminalSize();
my @log_levels = qw(ERROR-HL ERROR WARN-HL WARN INFO-HL INFO DEBUG-HL DEBUG TRACE-HL TRACE 5xx-HL 5xx 4xx-HL 4xx 3xx-HL 3xx 2xx-HL 2xx 1xx-HL 1xx );			# Define log bucket printing order
my %blocks = (
    'A' => '█',    # Full block
    'B' => '▓',    # Heavy shading
    'C' => '▒',    # Medium shading
    'D' => '░',    # Light shading
    'E' => '▪',    # Black Square
    'F' => '▫',    # White Square
    'G' => '☰',    # Menu
    'H' => '•',    # Bullet Point
    'I' => '■',    # Black Very Heavy
    'J' => '□'     # White square
);
my $default_chart_block = "A";

# TO DO:
# - add a summary table at the end showing totals in the various buckets (start, end time, total duration in time of logs, average logs per minute, second, total of all of the bucket occurences, size of the file, lines processed, lines omitted/didn't match)
# - add a feature to include/exclude HTTP status codes which would need to be converted to RegEx as 4xx isn't going to be found
# - add feature to auto-calculate time window bucket based on terminal height and min and max time range in file (this would be based on previous file reads, or better filesize)
# - add a feature allowing for auto-detection of file type, and hence analysis pattern/options to use
# - add a feature to allow gfor grouping of various other log categories like ThreadPool or Object context (instead of errors)
# - performance improvement: leverage a temp file with cached file stats (log start/end times, num lines, ...) to start/stop reading the file when provided start/end time ranges based on guesses around filesize

## LOG STRUCTURES ##
# RAC client: [2025-02-04T12:06:22.784] [TRACE] MessageStream - handling data from GAS with chunk size: 88
# TWX logs: 2025-02-04 12:05:57.481+0000 [L: DEBUG] [O: c.t.p.p.StreamEntryProcessor] [I: ] [U: ] [S: ] [P: ] [T: pool-6-thread-3] Stream Entry Block Size Threshold Reached...
# Tomcat access logs: 10.224.212.63 - - [02/Feb/2025:00:00:11 +0000] "GET /Thingworx/Metrics?x-thingworx-session=false HTTP/1.1" 200 17626 295

my %colors = (
    'ERROR' => "\033[0;31m",
    'WARN'  => "\033[0;33m",
    'INFO'  => "\033[0;32m",
    'DEBUG' => "\033[0;34m",
    'TRACE' => "\033[0;35m",
    '1xx'  => "\033[0;34m",
    '2xx'  => "\033[0;32m",
    '3xx'  => "\033[0;35m",
    '4xx'  => "\033[0;33m",
    '5xx'  => "\033[0;31m",
    'NC'    => "\033[0m",
    'black' => "\033[30m\033[49m",
    'red' => "\033[31m\033[49m",
    'green' => "\033[32m\033[49m",
    'yellow' => "\033[33m\033[49m",
    'blue' => "\033[34m\033[49m",
    'magenta' => "\033[35m\033[49m",
    'cyan' => "\033[36m\033[49m",
    'white' => "\033[36m\033[49m",
    'bright-black' => "\033[90m\033[109m",
    'bright-red' => "\033[91m\033[109m",
    'bright-green' => "\033[92m\033[109m",
    'bright-yellow' => "\033[93m\033[109m",
    'bright-blue' => "\033[94m\033[109m",
    'bright-magenta' => "\033[95m\033[109m",
    'bright-cyan' => "\033[96m\033[109m",
    'bright-white' => "\033[97m\033[109m",
    'black-underline' => "\033[4;30m",
    'red-underline' => "\033[4;31m",
    'green-underline' => "\033[4;32m",
    'yellow-underline' => "\033[4;33m",
    'blue-underline' => "\033[4;34m",
    'magenta-underline' => "\033[4;35m",
    'cyan-underline' => "\033[4;36m",
    'white-underline' => "\033[4;37m"
);

# ANSI code for yellow background
my $white_fg  = "\033[1;37m"; # Bright White Foreground
#my $yellow_bg = "\033[43m";  # Yellow background
my $yellow_bg = "\033[48;5;229m";  # Very pale yellow background
#my $yellow_bg = "\033[48;5;222m";  # Light gold background
#my $yellow_bg = "\033[48;5;216m";  # Pastel yellow background
my $reset_all    = "\033[0m";  # Reset ALL attributes

# Create new keys with -HL and yellow background
foreach my $key (keys %colors) {
    next if $key eq 'NC';  # Skip NC
    my $new_key = "$key-HL";
    #$colors{$new_key} = "$colors{$key}$yellow_bg";			# Apply a yellow background on top of the existing FG color
    
   my $original_fg_color = $colors{$key}; # Get original foreground

    # Extract the original colour code from the FG code
    $original_fg_color =~ s/^\e\[(.*?)m/$1/g;

    my $bg_color_256;
    if ($original_fg_color eq "0;31") {    # Red
        $bg_color_256 = "196";            # 256-color code for Red
    } elsif ($original_fg_color eq "0;33") { # Yellow
        $bg_color_256 = "226";            # 256-color code for Yellow
    } elsif ($original_fg_color eq "0;32") { # Green
        $bg_color_256 = "46";             # 256-color code for Green
    } elsif ($original_fg_color eq "0;34") { # Blue
        $bg_color_256 = "21";             # 256-color code for Blue
    } elsif ($original_fg_color eq "0;35") { # Magenta
        $bg_color_256 = "201";            # 256-color code for Magenta
    } else {
        $bg_color_256 = "0";              # Black background as default
    }

    #$colors{$new_key} = "\033[48;5;${bg_color_256}m${white_fg}";
    $colors{$new_key} = "$colors{$key}\033[48;5;${bg_color_256}m";
  #  print "$original_fg_color\n";
}

#foreach my $key (keys %colors){
#    print "Testing: $key\n";
#    print "$colors{$key}This is $key with \$colors{'NC'} $reset_all\n";
#}


## SUBS ##

sub print_title {
    my $title = <<"END";
\033[0;37m
\033[90m\033[109m
──────────────────────────────────────────────────────────────────────────────────────────────

   ,:: slt ::' simple log timeline  --  by Greg Eva // geva\@ptc.com || gregeva\@gmail.com

──────────────────────────────────────────────────────────────────────────────────────────────
\033[0m
END
    print $title;
    return;
}

sub print_version_and_usage {
    my ( $error_reason ) = @_;
    print "Usage: $0 --bucket-size | -bs <minutes> <file1> <file2> ...\n";
    print "\n  Error: $error_reason\n";
    return;
}

sub adapt_to_command_line_options {
    # Get command-line options
    GetOptions(
        'bucket-size|bs=i' => \$bucket_size_minutes,
        'pause|p' => \$pause_output,
        'hide-values|hv' => \$hide_values,
        'start|st=s' => \$range_start,
        'end|et=s' => \$range_end,
        'exclude|e=s' => \$exclude_regex,
        'include|i=s' => \$include_regex,
        'highlight|h=s' => \$highlight_regex,
        'omit-empty|oe' => \$omit_empty,
        'seconds|s' => \$print_seconds,
        'milliseconds|ms' => \$print_milliseconds
    ) or die print_version_and_usage( "required options not provided" ); 

    $range{'start'} = $range_start if defined( $range_start );
    $range{'end'} = $range_end if defined(  $range_end );

    # Ensure at least one file is provided
    @in_files = @ARGV;
    die print_version_and_usage( "unable to open any files" ) unless @in_files;

# Should include cleaner handling of possible problems with entering command line options
#eval {
#    die "Something bad happened";
#};
#if ($@) {
#    # Handle the error here
#    print "Caught the error: $@";
#}
    $output_timestamp_format .= ":%S" if $print_seconds || $print_milliseconds;
    $bucket_size_seconds = $print_seconds ? $bucket_size_minutes : $bucket_size_minutes * 60;		# if they say they want milliseconds, we're going to assume that their bucket sizes are in seconds instead
    if( $print_seconds ) {
        $bucket_size_seconds = $bucket_size_minutes;
    } elsif( $print_milliseconds ) {
        $bucket_size_seconds = $bucket_size_minutes / 1000;
    } else {
        $bucket_size_seconds = $bucket_size_minutes * 60;
    }

    return;
}

# Parse various possible formats for input of start and end times and set associated filter variables
sub calculate_start_end_filter_timestamps {
    my ( $log_time ) = @_;
    my $log_date = $log_time->epoch() - ( $log_time->hour * ( 60 * 60 ) + $log_time->minute * 60 + $log_time->second );

    foreach my $key (keys %range) {
        my $value = $range{$key};  # Get the value (either $range_start or $range_end)
        next unless length( $range{$key} ) > 0;
        my $epoch_value;  # Variable to store the epoch time

        # Convert timestamp to epoch seconds using Time::Piece -- WARNING -- use of strptime can cause timezone problems, it appears to work here as I've manually set TZ to UTC on input
        if ( $value =~ /^\d{4}-\d{2}-\d{2} \d{2}:\d{2}:\d{2}/ ) {
            $epoch_value = Time::Piece->strptime( $value, "%Y-%m-%d %H:%M:%S" )->epoch;
        } elsif ( $value =~ /^\d{4}-\d{2}-\d{2} \d{2}:\d{2}/ ) {
            $epoch_value = Time::Piece->strptime( $value, "%Y-%m-%d %H:%M" )->epoch;
        } elsif ( $value =~ /^\d{4}-\d{2}-\d{2}/ ) {
            $epoch_value = Time::Piece->strptime( $value, "%Y-%m-%d" )->epoch;
        } elsif ( $value =~ /^\d{2}:\d{2}:\d{2}/ ) {
            $epoch_value = $log_date + Time::Piece->strptime( $value, "%H:%M:%S" )->epoch;
        } elsif ( $value =~ /^\d{2}:\d{2}/ ) {
            $epoch_value = $log_date + Time::Piece->strptime( $value, "%H:%M" )->epoch;
        } else {
            print "Warning: unhandled date/time format - option not taken into account\n";
        }

        #print "Epoch value in seconds: $epoch_value\n";
        $range_epoch{$key} = $epoch_value if defined( $epoch_value );  # Store the epoch value in the hash
    }
    return 1;
}

sub adapt_to_terminal_settings {
    $terminal_width //= 80; # Default to 80 if terminal width cannot be determined
    $terminal_height //= 24; # Default to 24 if terminal height cannot be determined

    # Auto-adjust the bucket size based on the terminal height (larger terminal can handle more rows)
    if( $terminal_height <= 24 ) {
        $bucket_size_minutes = 60;
    } elsif( $terminal_height <= 45 ) {
        $bucket_size_minutes = 30;
    } elsif( $terminal_height <= 65 ) {
        $bucket_size_minutes = 10;
    } elsif( $terminal_height <= 85 ) {
        $bucket_size_minutes = 5;
    } elsif( $terminal_height > 85 ) {
        $bucket_size_minutes = 1;
    } else {
        $bucket_size_minutes = 60;
    }
    #print "Terminal is: $terminal_width x $terminal_height -- Bucket size: $bucket_size_minutes\n";
    return;
}

sub pause_for_keypress {
    my ($prompt) = @_;  # Get the optional prompt message
    $prompt = "Press any key to continue (or Q to quit)..." unless defined $prompt;
    print "\033[0;44m$prompt\033[0m";

    ReadMode 4;  # Turn off line buffering
    my $key = ReadKey(0);
    ReadMode 0;  # Restore normal input mode

    if (defined $key) {
        if ($key eq "q" || $key eq "Q") {
            print "\r";
            print " " x length( $prompt );
            print "\r\033[0;35mExiting program.\033[0m\n";
            exit 0;
        } else {
            print "\r";
            print " " x length( $prompt );
            print "\r";
            return 1;  # Return a value indicating "continue"
        }
    } else {
        print "\r";
        print " " x length( $prompt );
        print "\r";
        return 1;
    }
}

# Parse logs, bucket by time, and count log levels
sub read_and_process_logs {
    # BUG: this should be able to take multiple files as inputs using * but it doesn't work, same for including multiple files
    foreach my $in_file (@in_files) {
        open my $fh, '<', $in_file or die "Cannot open file: $in_file";
        my $line_number = 0;
        my $range_filter_initialized = 0;
        my %month_map = ( Jan => 1, Feb => 2, Mar => 3, Apr => 4, May => 5, Jun => 6, Jul => 7, Aug => 8, Sep => 9, Oct => 10, Nov => 11, Dec => 12 );

        while (<$fh>) {
            $line_number++;
            my ( $timestamp_str, $log_level, $category_bucket ) = "";
            my ( $is_line_match, $is_access_log, $match_type, $status_code, $bytes_sent, $request_duration ) = 0;
            my $timestamp;
            $total_lines_read++;

            if ($line_number % 997 == 0) {	# only print processing status update every X lines processed
                printf("\rProcessing line %d in file %s", $line_number, $in_file);
                $| = 1; # Flush output
            }
            # ORIGINAL #if (my ($timestamp_str, $log_level) = $_ =~ /^(\d{4}-\d{2}-\d{2} \d{2}:\d{2}:\d{2})\.\d{3}\+\d{4} \[L: ([^\]]*)\]/) {

#            if (my ($timestamp_str, $log_level) = $_ =~ /^(\d{4}-\d{2}-\d{2} \d{2}:\d{2}:\d{2})\.\d{3}\+\d{4} \[L: ([^\]]*)\]/) {
#           #if( $_ = /^[\[]?(?<timestamp_str>\d{4}-\d{2}-\d{2}[ T]\d{2}:\d{2}:\d{2})\.?(?<milliseconds>\d{3})?.*? \[[L: ]*(?<log_level>[^\]]*)\]/ ) {
#                $timestamp_str =~ tr/T/ /;
#                my $category_bucket = $log_level;
#                # Convert timestamp to epoch seconds using Time::Piece
#    #            my $timestamp = Time::Piece->strptime($timestamp_str, "%Y-%m-%d %H:%M:%S")->epoch;
#                my $timestamp = DateTime->new(
#                    year      => substr($timestamp_str, 0, 4),
#                    month     => substr($timestamp_str, 5, 2),
#                    day       => substr($timestamp_str, 8, 2),
#                    hour      => substr($timestamp_str, 11, 2),
#                    minute    => substr($timestamp_str, 14, 2),
#                    second    => substr($timestamp_str, 17, 2),
#                    time_zone => 'UTC',
#                );

            if ( ($timestamp_str, $category_bucket) = $_ =~ /^(\d{4}-\d{2}-\d{2} \d{2}:\d{2}:\d{2}\.\d{3})\+\d{4} \[L: ([^\]]*)\]/) {
                $is_line_match = 1;
                $match_type = 1;			# this is matching ThingWorx standard log format 2025-02-04 12:05:57.481+0000 [L: DEBUG] 
            } elsif ( ($timestamp_str, $category_bucket ) = $_ =~ /^[\[]?(\d{4}-\d{2}-\d{2}[ T]\d{2}:\d{2}:\d{2}\.\d{3}).*? \[[L: ]*([^\]]*)\]/ ) {
                $is_line_match = 1;
                $match_type = 2;			# this is matching RAC client log format [2025-02-04T12:06:22.784] [TRACE]
                $timestamp_str =~ tr/T/ /;
            } elsif ( (undef, $timestamp_str, $category_bucket, $bytes_sent, $request_duration) = $_ =~ /^(.+? ){3}[\[]([^\]]+)[\]] "[^"]+" (\d{3}) (\d+) (\d+)/ ) {
                $is_line_match = 1;
                $match_type = 3;			# this is matching Tomcat access log format with service execution time 43.52.82.172 - - [02/Feb/2025:00:00:11 +0000] "GET /Thingworx/Metrics?x-thingworx-session=false HTTP/1.1" 200 17626 295
                $is_access_log = 1;
                $status_code = $category_bucket;
                $category_bucket =~ s/(\d)\d{2}/$1xx/;	# bucket HTTP status codes into their primary families
                $timestamp_str =~ s/ \+\d{4}$//;	# chop off the timezone offset
            } elsif ( (undef, $timestamp_str, $category_bucket, $bytes_sent) = $_ =~ /^(.+? ){3}[\[]([^\]]+)[\]] "[^"]+" (\d{3}) (\d+)/ ) {
                $is_line_match = 1;
                $match_type = 4;			# this is matching Tomcat access log format with service execution time 43.52.82.172 - - [02/Feb/2025:00:00:11 +0000] "GET /Thingworx/Metrics?x-thingworx-session=false HTTP/1.1" 200 17626
                					# this is matching Nginx ingress access log format 3.133.31.181 - - [18/Nov/2024:14:19:55 +0000] "GET / HTTP/1.1" 404 431 "-" "WizDynamicScanner/1.0"
                $is_access_log = 1;
                $status_code = $category_bucket;
                $category_bucket =~ s/(\d)\d{2}/$1xx/;	# bucket HTTP status codes into their primary families
                $timestamp_str =~ s/ \+\d{4}$//;	# chop off the timezone offset
            } elsif ( ($timestamp_str, $category_bucket) = $_ =~ /^{"\@timestamp":"([^"]*).*"level":"([^"]*)/ ) {
                $is_line_match = 1;
                $match_type = 5;			# this matches Connection Server JSON formatted logs : {"@timestamp":"2025-02-02T21:03:06.725+00:00","@version":1,"message":"Error encountered, closing WebSocket: endpointId=2608459","logger_name":"com.thingworx.connectionserver.alwayson.AbstractClientEndpoint","thread_name":"vert.x-eventloop-thread-16","level":"WARN","level_value":30000,"stack_trace":"io.vertx.core.http.HttpClosedException: Connection was closed\n"}
                $timestamp_str =~ s/\+\d{2}:\d{2}$//;	# chop off the timezone offset
                $timestamp_str =~ tr/T/ /;
            }


            #print "\033[0;32m[$timestamp_str|$category_bucket] [$is_line_match/$match_type] \033[0m$_";
            #$| = 1; # Flush output

            if( $is_line_match ) {
                if( $match_type == 1 || $match_type == 2 || $match_type == 5 ) {
                    $timestamp = DateTime->new(
                        year      => substr($timestamp_str, 0, 4),
                        month     => substr($timestamp_str, 5, 2),
                        day       => substr($timestamp_str, 8, 2),
                        hour      => substr($timestamp_str, 11, 2),
                        minute    => substr($timestamp_str, 14, 2),
                        second    => substr($timestamp_str, 17, 2),
                        time_zone => 'UTC',
                    );
                } elsif( $match_type == 3 || $match_type == 4 ) {
                    my ($day, $month_str, $year, $hour, $minute, $second) = $timestamp_str =~ m/(\d{2})\/([A-Za-z]+)\/(\d{4}):(\d{2}):(\d{2}):(\d{2})/;
                    my $month = $month_map{$month_str};
                    $timestamp = DateTime->new(
                        year      => $year,
                        month     => $month,
                        day       => $day,
                        hour      => $hour,
                        minute    => $minute,
                        second    => $second,
                        time_zone => 'UTC',
                    );

#                    print "\033[0;35m[%d]\033[0m", $timestamp->epoch();
#                    $| = 1; # Flush output
                }

                $range_filter_initialized = calculate_start_end_filter_timestamps( $timestamp ) unless $range_filter_initialized;

                # a number of conditions determine if this read row should be skipped
                next if( $timestamp->epoch() < $range_epoch{'start'} || $timestamp->epoch() >= $range_epoch{'end'} );
                next if( defined( $exclude_regex ) && /$exclude_regex/ );
                next if( defined( $include_regex ) && !/$include_regex/ );

                # determine if this line should be highlighted
                $category_bucket .= "-HL" if( defined( $highlight_regex ) && /$highlight_regex/ );


                # sets earliest and latest timestamp values being displayed (after the range filtering selection)
                $output_timestamp_min = $timestamp->epoch() if !defined( $output_timestamp_min ) || $output_timestamp_min > $timestamp->epoch();
                $output_timestamp_max = $timestamp->epoch() if !defined( $output_timestamp_max ) || $output_timestamp_max < $timestamp->epoch();

                my $bucket = int($timestamp->epoch() / $bucket_size_seconds) * $bucket_size_seconds;
                $log_counts{$bucket}{$category_bucket}{count}++;
                $log_counts{$bucket}{$category_bucket}{duration} += $request_duration if $request_duration;
                # if not already set, configure the block character to use
                $log_counts{$bucket}{$category_bucket}{'block'} = ( !defined $log_counts{$bucket}{$category_bucket}{block} && !$is_access_log ) ? $blocks{'I'} : $blocks{'J'};
            }
        }
        close $fh;
    }
    print "\r$colors{'bright-green'}Processing complete.$colors{'NC'}";
    print " " x ( $terminal_width - length( "Processing complete." ) );
    print "\n\n";

    return;
}

sub normalize_data_for_output {
    # Determine the maximum total log messages for any time window
    my $max_total_count = 0;

    # Go through all of the buckets between the earliest and latest times and ensure that they have empty data to print time buckets with no data
    unless( $omit_empty ) {
        my $start_bucket = int($output_timestamp_min / $bucket_size_seconds) * $bucket_size_seconds;
        for (my $bucket = $start_bucket; $bucket <= $output_timestamp_max; $bucket += $bucket_size_seconds) {
            $log_counts{$bucket}{'empty'}{'count'} = 0;
        }
    }

    # Example usage (to demonstrate the values):
#    foreach my $bucket (sort keys %log_counts) {
#        print "Bucket: $bucket, Empty Count: ", $log_counts{$bucket}{'empty'}{'count'}, "\n";
#    }

    foreach my $bucket (keys %log_counts) {
        my $total_count = 0;
        foreach my $category_bucket (keys %{$log_counts{$bucket}}) {
            $total_count += $log_counts{$bucket}{$category_bucket}{count};
        }
        $max_total_count = $total_count if $total_count > $max_total_count;
    }

    # Calculate the maximum length of log level titles and counts
    if( !$hide_values ) {
        foreach my $bucket (keys %log_counts) {
            my $bucket_legend_length = 0;
            foreach my $category_bucket (keys %{$log_counts{$bucket}}) {
                my $count = $log_counts{$bucket}{$category_bucket}{count};
                next unless $count > 0;				# don't include the empty bucket who's purpose is to normalize the buckets represented
		
                my $title_length = $category_bucket =~ /-HL$/ ? length(" $count ") : length("$category_bucket: $count ");
                $bucket_legend_length += $title_length;
            }
            $legend_length = $bucket_legend_length if $bucket_legend_length > $legend_length;
        }
    }

    # Normalize counts to fit terminal width
    my $max_graph_width = $terminal_width - $legend_length - 2 - 4 - length( $output_timestamp_format );    # static values based on timestamp length, some white space, maybe a vertical line spacer
    $max_graph_width -= 4 if $print_milliseconds;								# remove 4 characters from the X size of graph to leave room for millisecond output
    foreach my $bucket (keys %log_counts) {
        foreach my $category_bucket (keys %{$log_counts{$bucket}}) {
            my $count = $log_counts{$bucket}{$category_bucket}{count};
            my $scaled_count = int(($count / $max_total_count) * $max_graph_width);
            $log_counts{$bucket}{$category_bucket}{scaled_count} = $scaled_count;
            $category_totals{$category_bucket} += $count;							# tally the totals for each category bucket
            $total_lines_included += $count;
            $total_lines_highlighted += $count if $category_bucket =~ /-HL$/;
        }
    }
    return;
}

sub print_bar_graph {
    my $lines_printed = 0;
    foreach my $bucket (sort keys %log_counts) {
        my $bucket_time_str = strftime($output_timestamp_format, gmtime($bucket));
        $bucket_time_str .= sprintf ".%03d", ($bucket-int($bucket))*1000 if $print_milliseconds;
        print "$bucket_time_str ";
        my $log_details = "";
        my $legend_length_bucket = 0;
        foreach my $category_bucket (@log_levels) {
            if (exists $log_counts{$bucket}{$category_bucket}) {
                my $count = $log_counts{$bucket}{$category_bucket}{count};
                my $color = $colors{$category_bucket} // $colors{'NC'};
                if( $category_bucket =~ /-HL$/ ) {
                    $log_details .= "${color}$count$colors{'NC'} ";
                    $legend_length_bucket += length("$count ");
                } elsif( $count < 1 ) {					 # don't include the empty bucket who's purpose is to normalize the buckets represented
                    next;
                } else {
                    $log_details .= "$color$category_bucket: $count$colors{'NC'} ";
                    $legend_length_bucket += length("$category_bucket: $count ");
                }
            }
        }
        print $log_details unless $hide_values;
        my $padding = $legend_length - $legend_length_bucket;
        print " " x ($padding >= 0 ? $padding : 0);
        print "│ ";
        foreach my $category_bucket (@log_levels) {
            if (exists $log_counts{$bucket}{$category_bucket}) {
                my $scaled_count = $log_counts{$bucket}{$category_bucket}{scaled_count};
                my $color = $colors{$category_bucket} // $colors{'NC'};
 #               print "$color" . ('█' x $scaled_count) . "$colors{'NC'}";
                # the following provides a mechanism for changing the look of the bars for access logs compared to other logs
                print "$color" . ( $category_bucket =~ /^[12345]xx(-HL)?$/ ? $blocks{'I'} : $blocks{$default_chart_block} ) x $scaled_count . "$colors{'NC'}";
            }
        }
#print $log_counts{$bucket}{$category_bucket}{duration};
        print "\n";
        pause_for_keypress() if( $pause_output && $lines_printed++ > 1 && $lines_printed % ( $terminal_height - 1 ) == 0 );
    }
    return;
}

sub print_summary_table {
    my $category_column_width = 15;
    my $count_column_width = 10;
    my $table_format = "  %-${category_column_width}s %${count_column_width}d\n";

    print "\n\n";
    print "  " . "─" x ( $category_column_width + $count_column_width + 1 ) . "\n";
    printf "  " . "%-${category_column_width}s %${count_column_width}s\n", "Category", "Total"; 
    print "  " . "─" x ( $category_column_width + $count_column_width + 1 ) . "\n";

    foreach my $category_bucket (@log_levels) {
        next unless $category_totals{$category_bucket};
	
        my ( $legend_title ) =  "$category_bucket:" . " " x ( 12 - length( $category_bucket ) );
        #print "$legend_title$category_totals{$category_bucket}\n"; 
        #printf "%-${category_column_width}s %${count_column_width}d\n", $category_bucket, $category_totals{$category_bucket}; 
        printf $table_format, $category_bucket, $category_totals{$category_bucket}; 

    }
    printf $table_format, "HIGHLIGHTED", $total_lines_highlighted if defined $highlight_regex;
    printf $table_format, "LINES INCLUDED", $total_lines_included; 
    printf $table_format, "LINES READ", $total_lines_read; 
    print "  " . "─" x ( $category_column_width + $count_column_width + 1 ) . "\n";
    print "\n";

    return;
}

## MAIN ##

print_title();
adapt_to_terminal_settings(); 
adapt_to_command_line_options();
#calculate_start_end_filter_timestamps();
read_and_process_logs();
normalize_data_for_output();
print_bar_graph();
print_summary_table();

print "\n";
exit;
